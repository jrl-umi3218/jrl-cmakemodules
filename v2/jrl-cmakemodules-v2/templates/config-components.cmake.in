# Generated by CMake @CMAKE_VERSION@ for @PROJECT_NAME@
@PACKAGE_INIT@
message(DEBUG "Welcome to @PROJECT_NAME@ !")

# valid components that can be searched for
set(@PROJECT_NAME@_supported_components @PROJECT_COMPONENTS@)
message(DEBUG "   Valid components for @PROJECT_NAME@: ${@PROJECT_NAME@_supported_components}")
message(DEBUG "   User is requesting components: ${@PROJECT_NAME@_FIND_COMPONENTS}")

# if no components were requested, add them all.
if(@PROJECT_NAME@_FIND_COMPONENTS STREQUAL "")
    message(DEBUG "   No components were requested, including all supported components.")
    set(@PROJECT_NAME@_FIND_COMPONENTS ${@PROJECT_NAME@_supported_components})
endif()

# iterate through components attempting to be found
foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})
    message(DEBUG "       Adding component ${component}")

    if(${component} IN_LIST @PROJECT_NAME@_supported_components)
        message(DEBUG "           Including ${CMAKE_CURRENT_LIST_DIR}/${component}/targets.cmake")
        include("${CMAKE_CURRENT_LIST_DIR}/${component}/targets.cmake")

        message(DEBUG "           Including ${CMAKE_CURRENT_LIST_DIR}/${component}/dependencies.cmake")
        include("${CMAKE_CURRENT_LIST_DIR}/${component}/dependencies.cmake")

        set(@PROJECT_NAME@_${component}_FOUND True)
    else()
        set(@PROJECT_NAME@_FOUND False)
        string(REPLACE ";" ", " @PROJECT_NAME@_components_pp "${@PROJECT_NAME@_supported_components}")
        set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "Unsupported component '${component}'.\n Supported components are:\n ${@PROJECT_NAME@_components_pp}")
        break()
    endif()
endforeach()

@PACKAGE_CONFIG_EXTRA_CONTENT@